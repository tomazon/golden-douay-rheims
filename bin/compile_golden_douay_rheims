#! /usr/bin/env perl

use strict;
use warnings;

use Data::Dumper;
use File::Copy;
use File::Slurp;
use Getopt::Long;
use IPC::Run3;
use String::Diff qw( diff );
use String::ShellQuote;
use String::Trim;
use Text::Capitalize;
use YAML;
use utf8;

use feature 'say';

binmode STDOUT, ":raw";

require "./sources/lib/perl5/shared.pl";

my $book_order_file = './etc/book_order.yaml';
my $top_out_dir = "./library";
my $golden_out_dir = "$top_out_dir/golden-douay-rheims";

my %source_dirs = (
    'johnblood' => './sources/johnblood_haydock/out',
    'pg1581'            => './sources/pg1581/out',
    'drbo'              => './sources/drbo/out',
    'cbol'              => './sources/cbol_douay-rheims/out',
    'vulsearch'         => './sources/vulsearch_vulgate/out',
    'pg8300_info'       => './sources/pg8300/etc/by_id.yaml',
    'vscodes_count'     => './sources/vulsearch_vulgate/out_vscode_counts',
);


my $drbo_dir = $source_dirs{drbo};
my $pg1581_dir = $source_dirs{pg1581};

my @books = book_list($book_order_file, qw( OT NT ));

for my $book_id (@books) {
    next unless book_has_red_letters($book_id);
    my $out_file = "$golden_out_dir/$book_id.yaml";
    my $book = {
        book_id => $book_id,
        titles => {},
        chapters => {},
        intro => "",
    };
    build_chapters($book, $drbo_dir, $pg1581_dir, $book_id);
    $book->{intro} = fetch_book_field($pg1581_dir, $book_id, 'book_intro');
    $book->{titles} = get_titles(\%source_dirs, $book_id);

    YAML::DumpFile($out_file, $book);
}

say "Reenable CP";
#copy_vulsearsh_files($source_dirs{'vulsearch'}, "$top_out_dir/vulsearch_vulgate");

###########################################################################

sub book_yaml_file {
    my ($dir, $book_id) = @_;
    "$dir/$book_id.yaml";
}

sub build_chapters {
    my ($book, $drbo_dir, $pg1581_dir, $book_id) = @_;
    my $drbo_file =   "$drbo_dir/$book_id.yaml";
    my $pg1581_file = "$pg1581_dir/$book_id.yaml";
    my $vscodes_count_file = "$source_dirs{vscodes_count}/$book_id.yaml";

    my $footnote_fixes = YAML::LoadFile('./etc/footnote_fixes.yaml');
    my $red_letter_map = {};
    if (book_has_red_letters($book_id)) {
        $red_letter_map  = YAML::LoadFile("./etc/red_letters/$book_id.yaml");
    }

    say "Processing: $book_id";

    die "No such directory: $drbo_dir" unless -d $drbo_dir;
    die "No such directory: $pg1581_dir" unless -d $pg1581_dir;
    die "No_such_file: $drbo_file\n" unless -f $drbo_file;
    die "No_such_file: $pg1581_file\n" unless -f $pg1581_file;

    my $drbo_data     = YAML::LoadFile($drbo_file);
    my $pg1581_data   = YAML::LoadFile($pg1581_file);
    my $vscodes_count = YAML::LoadFile($vscodes_count_file);

    for my $c_index (combined_keys($drbo_data->{chapters}, $pg1581_data->{chapters})) {
        my $c_tag = num_to_tag('c', $book_id, $c_index);

        my $drbo_chap   = $drbo_data->{chapters}->{$c_index};
        my $pg1581_chap = $pg1581_data->{chapters}->{$c_index};

        $book->{chapters}->{$c_index} = { 'verses' => {} };
        my $chap_out = $book->{chapters}->{$c_index};

        $chap_out->{intro} = $pg1581_chap->{intro};
        $chap_out->{footnotes} = {};

        for my $v_index (combined_keys($drbo_chap->{verses}, $pg1581_chap->{verses})) {
            my $v_num = tag_to_num($v_index);
            my $v_tag = num_to_tag('v', $c_tag, $v_num);
            my $drbo_vers   = $drbo_chap->{verses}->{$v_index};
            my $pg1581_vers = $pg1581_chap->{verses}->{$v_index};

            if (!defined($drbo_vers) || $drbo_vers eq '') {
                warn "Only in $pg1581_file: $v_tag\n";
                next;
            }
            if (!defined($pg1581_vers) || $pg1581_vers eq '') {
                warn "Only in $drbo_file: $v_tag\n";
                next;
            }

            my ($pg1581_tag, $pg1581_v, $pg1581_dis, $pg1581_text) = split(/\s*\|\s*/, $pg1581_vers);
            my ($drbo_tag,   $drbo_v,   $drbo_dis,   $drbo_text)   = split(/\s*\|\s*/, $drbo_vers);

            die "Error: drbo_tag & v_tag don't match: '$drbo_tag' != '$v_tag'\n" if $drbo_tag   ne $v_tag;
            die "Error: pg1581_tag & v_tag don't match: '$pg1581_tag' != '$v_tag'\n" if $pg1581_tag ne $v_tag;
            die "Error: drbo_v & v_num don't match: '$drbo_v' != '$v_num'\n" if $drbo_v ne $v_num;
            die "Error: pg1581_v & v_num don't match: '$pg1581_v' != '$v_num'\n" if $pg1581_v ne $v_num;

            my $display_v = choose_display_v($v_tag, $v_num, $drbo_dis, $pg1581_dis);
            my $text = choose_verse_text($v_tag, $drbo_text, $pg1581_text);

            if (defined($drbo_chap->{footnotes}->{$v_index}) || defined($pg1581_chap->{footnotes}->{$v_index})) {
                $chap_out->{footnotes}->{$v_index} = choose_footnotes($v_tag, $v_index, $drbo_chap, $pg1581_chap, $footnote_fixes);
                $text = make_footnote_tags($chap_out, $v_tag, $v_index, $text);
            }

            $text = apply_vscodes($text, $vscodes_count, $c_index, $v_index);
            if (book_has_red_letters($book_id)) {
                $text = apply_redletters($v_tag, $text, $red_letter_map);
            }

            my $entry = join(' | ', $v_tag, $v_num, $display_v, $text);
            $chap_out->{verses}->{$v_index} = $entry;

        }
    }
}

sub make_footnote_tags {
    my ($chap, $v_tag, $v_index, $text) = @_;
    my $index=0;
    return $text if $chap->{footnotes}->{$v_index} eq "";
    for my $entry (@{$chap->{footnotes}->{$v_index}}) {
        my $match = $entry->{match};
        if ($text =~ s/($match)/\{FOOTNOTE:BEGIN $index}$1\{FOOTNOTE:END\}/i) {
            $chap->{verses}->{$v_index} = $text;
        } else {
            say "FIX_FOOTNOTE:\n  $v_tag:\n    $index:\n      match: ''  # was '$match' -- $text\n";
        }
        $index++;
    }

    $text
}



sub apply_vscodes {
    my ($text, $vscodes_count, $c_index, $v_index) = @_;
    my $counts = $vscodes_count->{chapters}->{$c_index}->{verses}->{$v_index};
    if (defined $counts) {
        $text .= '{VS:P}'  if $counts->{ending_p};
        $text .= '{VS:BR}' if $counts->{ending_br};
        $text .= '{VS:VERSE:END}' if $counts->{ending_verseend};
        $text .= '{VS:VERSE:END}{VS:P}' if $counts->{ending_verseend_p};
        $text = "{VS:VERSE:BEGIN}$text" if $counts->{beginning_versebegin};
    }

    $text
}

sub apply_redletters {
    my ($v_tag, $text, $map) = @_;

    my $tag = $v_tag;
    $tag =~ s/^[A-Z]{3}://;

    if (my $rule = $map->{$tag}) {
        trim($rule);

        if ($rule eq 'ALL') {
            $text = "{RED:BEGIN}${text}{RED:END}";
        } elsif ($rule =~ /^(.+)\s+\.\.\.$/) {
            my $quote=$1;
            trim($quote);
            die "$tag: Multiple red_letter quotes, Stopped" if $quote =~ /\.\.\./;
            $text =~ s/($quote.*)$/\{RED:BEGIN}$1\{RED:END}/i
                || warn "$v_tag: Failed to apply red_letters | rule = $rule | text = $text |";
        } elsif ($rule =~ /^\.\.\.\s+(.+)$/) {
            my $quote=$1;
            trim($quote);
            die "$tag: Multiple red_letter quotes, Stopped" if $quote =~ /\.\.\./;
            $text =~ s/^(.+$quote)/\{RED:BEGIN}$1\{RED:END}/i
                || warn "$v_tag: Failed to apply red_letters | rule = $rule | text = $text |";
        } elsif ($rule =~  /^(.+\S)\s\.\.\.\s(\S.+)$/) {
            my ($begin, $end) = ($1, $2);
            trim($begin);
            trim($end);
            die "$v_tag: Multiple red_letter quotes, Stopped" if $begin =~ /\.\.\./;
            die "$v_tag: Multiple red_letter quotes, Stopped" if $end =~ /\.\.\./;
            $text =~ s/\s(${begin}\s.+\s${end})\s/ \{RED:BEGIN}$1\{RED:END} /i
                || warn "$v_tag: Failed to apply red_letters | rule = $rule | text = $text |";
        } else {
            my $quote = $rule;
            trim($quote);
            die "$v_tag: Multiple red_letter quotes, Stopped" if $quote =~ /\.\.\./;
            $text =~ s/($quote)/\{RED:BEGIN}$1\{RED:END}/i 
                || warn "$v_tag: Failed to apply red_letters | rule = $rule | text = $text |";
        }
    }

    $text;
}

sub book_has_red_letters {
    my ($book_id) = @_;
    grep(/^$book_id$/, (qw( MAT MRK LUK JHN )));
}

sub choose_display_v {
    my ($v_tag, $v_num, $drbo_dis, $pg1581_dis) = @_;
    #TODO: use some logic

    $pg1581_dis;
}

sub choose_verse_text {
    my ($v_tag, $drbo_text, $pg1581_text) = @_;
    #TODO: use some logic

    $pg1581_text;
}

sub choose_footnotes {
    my ($v_tag, $v_index, $drbo_chap, $pg1581_chap, $footnote_fixes) = @_;
    my @out = ();
    #TODO: use some logic

    my $drbo_footnotes = $drbo_chap->{footnotes}->{$v_index};
    my $pg1581_footnotes = $pg1581_chap->{footnotes}->{$v_index};

    if (defined($drbo_footnotes)) {
        return format_footnotes($v_tag, $drbo_footnotes, $footnote_fixes);
    }
}

sub format_footnotes {
    my ($v_tag, $footnotes_for_verse, $footnote_fixes) = @_;
    my @out = ();

    my $i=0;
    for my $e (@{$footnotes_for_verse}) {
        $e->{match} = lc($e->{'quote'});
        if (defined(my $f = $footnote_fixes->{$v_tag}->{$i})) {
            $e->{match} = lc($f->{match}) if defined($f->{match});
        }
        push(@out, $e);
        $i++;
    }
    \@out;
}

sub copy_vulsearsh_files {
    my ($in_dir, $out_dir) = @_;
    print "Copying VulSearch (Vulgate) files:\n";
    for my $file (read_dir($in_dir)) {
        my $from_file = "$in_dir/$file";
        my $to_file = "$out_dir/$file";
        print "  CP $from_file  $to_file\n";
        copy($from_file, $to_file);
    }
    print "\n";
}

sub tag_to_num {
    my ($tag) = @_;
    $tag =~ s/^.+://;
    $tag =~ s/^0+//;

    $tag
}

sub get_titles {
    my ($source_dirs, $book_id) = @_;
    my $titles = {};
    my $names = {};

    for my $src (qw( cbol drbo pg1581 johnblood )) {
        my $title = fetch_book_field($source_dirs->{$src}, $book_id, 'book_title');
        if ($src eq 'pg1581' || $src eq 'johnblood') {
            $title = capitalize_title($title);
        }
        $titles->{$src} = $title;
    }
    {
        my $pg8300 = YAML::LoadFile($source_dirs->{'pg8300_info'});
        $titles->{pg8300} = $pg8300->{$book_id}->{title};
        $titles->{'pg8300_short'} = $pg8300->{$book_id}->{short};
    }
    $titles->{'cbol_abv'} = fetch_book_field($source_dirs->{'cbol'}, $book_id, 'book_abv');

    $titles
}

sub fetch_book_field {
    my ($dir, $book_id, $field) = @_;

    my $file_name = book_yaml_file($dir, $book_id);
    #say "LOADING: $file_name FIELD: $field\n";
    my $data = YAML::LoadFile($file_name);
    $data->{$field};
}

